-- Syntax of GHOPFL defined in LBNF format
-- To be processed into a parser and lexer using BNFC

entrypoints Environment, Exp, Prg;
comment     "#" ;

-- Tokens
token       Lam     ('λ' | '\\') ;
token       Conj    ('∧' | 'a''n''d') ;
token       Disj    ('∨' | 'o''r') ;
token       TNot    ('¬' | '!') ;
token       TLeq    ('≤' | '<''=') ;
token       TGeq    ('≥' | '>''=') ;
token       TLApp   ('⊙' | '(''.'')') ;
token       TSub    ('←' | '<''-') ;
token       TMatch  ('→' | '-''>') ;

-- Boolean expressions
BTrue.      BConst  ::= "true" ;
BFalse.     BConst  ::= "false" ;

-- Terms
Var.        Exp12   ::= Ident ;
Val.        Exp12   ::= Double ;
BVal.       Exp12   ::= BConst ;

-- Later modality (with applicative function LApp)
Next.       Exp11   ::= "next" Exp12 ;
Prev.       Exp11   ::= "prev" "{" Environment "}" "." Exp12 ;
-- Syntactic sugar for empty sublist
PrevE.      Exp11   ::= "prev" Exp12;
-- Syntactic sugar for prev{\x ← \x}.t with \x being all free vars in t
PrevI.      Exp11   ::= "prevI" Exp12;

Box.        Exp11   ::= "box" "{" Environment "}" "." Exp11 ;
BoxI.       Exp11   ::= "boxI" Exp12 ;

Unbox.      Exp11   ::= "unbox" Exp12 ;

-- Recursion
In.         Exp11   ::= "in" Exp12 ;
Out.        Exp11   ::= "out" Exp12 ;

-- Projections
Fst.        Exp11   ::= "fst" Exp12 ;
Snd.        Exp11   ::= "snd" Exp12 ;

-- Injections
InL.        Exp11   ::= "inL" Exp12 ;
InR.        Exp11   ::= "inR" Exp12 ;

-- (Delayed) application
App.        Exp10    ::= Exp10 Exp11 ;
LApp.       Exp10    ::= Exp10 TLApp Exp11;

-- Arithmetic
Pow.        Exp9    ::= Exp9 "^" Exp10 ;
Mul.        Exp8    ::= Exp8 "*" Exp9 ;
Div.        Exp8    ::= Exp8 "/" Exp9 ;
Add.        Exp7    ::= Exp7 "+" Exp8 ;
Sub.        Exp7    ::= Exp7 "-" Exp8 ;

-- Boolean
Eq.         Exp6    ::= Exp6 "=" Exp7 ;
Lt.         Exp6    ::= Exp6 "<" Exp7 ;
Gt.         Exp6    ::= Exp6 ">" Exp7 ;
Leq.        Exp6    ::= Exp6 TLeq Exp7 ;
Geq.        Exp6    ::= Exp6 TGeq Exp7 ;

Not.        Exp5    ::= TNot Exp6 ;
And.        Exp4    ::= Exp4 Conj Exp5 ;
Or.         Exp4    ::= Exp4 Disj Exp5 ;

Pair.       Exp3    ::= "[" Exp3 "," Exp3 "]";
Norm.       Exp3    ::= "normal" Exp3 ;

Ite.        Exp2    ::= "if" Exp4 "then" Exp7 "else" Exp7 ;
Match.      Exp1    ::= "match" Exp11 "{" Ident TMatch Exp1 ";" Ident TMatch Exp1 "}" ;

Abstr.      Exp     ::= Lam Ident "." Exp ;
Rec.        Exp     ::= "fix" Ident "." Exp ;
coercions Exp 12 ;

-- Program with definitions
DefProg.    Prg     ::= "let" Environment "in:" Exp ;
Prog.       Prg     ::= Exp ;

-- Environment passing
Assign.     Assignment  ::= Ident TSub Exp ;
Env.        Environment ::= [Assignment] ;
separator   Assignment ";" ;

